Value Filldown INTERFACE ([0-9\/]+)
Value Required NEIGHBOR_MAC_ADDRESS (\w+\.\w+\.\w+)
Value TTL_SECONDS (\d+)
Value NEIGHBOR_HOSTNAME_ID (.+)
Value NEIGHBOR_INTERFACE ([a-zA-Z0-9\/\s]+)
Value NEIGHBOR_PORT_VLAN_ID (\d+|none)
Value NEIGHBOR_MANAGEMENT_IP_ADDRESS (\d+\.\d+\.\d+\.\d+)
Value NEIGHBOR_LACP_INDEX (\d+)
Value NEIGHBOR_MAX_FRAME_SIZE (\d+)
Value NEIGHBOR_OPERATIONAL_MAU ([a-zA-Z0-9\-]+)
Value NEIGHBOR_CAPABILITIES (.+)

Start
  ^\w -> Continue.Record
  ^Local\s+port:\s+${INTERFACE}
  ^\s+\+\s+System\s+name\s+:\s+"${NEIGHBOR_HOSTNAME_ID}"
  ^\s+\+\s+Port\s+description\s+:\s+"${NEIGHBOR_INTERFACE}"
  ^\s+Enabled\s+capabilities\s*:\s+${NEIGHBOR_CAPABILITIES}
  ^\s+\+\s+Port\s+VLAN\s+ID:\s+${NEIGHBOR_PORT_VLAN_ID}
  ^\s+\+\s+Management\s+address\s+\(IPv4\):\s+${NEIGHBOR_MANAGEMENT_IP_ADDRESS}
  ^\s+\+\s+Link\s+aggregation:\s+aggregated\s+\(aggregated\s+port\s+ifIndex:\s+${NEIGHBOR_LACP_INDEX}\)
  ^\s+\+\s+Maximum\s+frame\s+size:\s+${NEIGHBOR_MAX_FRAME_SIZE}
  ^\s+Operational\s+MAU\s+type\s+:\s+${NEIGHBOR_OPERATIONAL_MAU}
  ^\s+Neighbor:\s+${NEIGHBOR_MAC_ADDRESS},\s+TTL\s+${TTL_SECONDS}
  ^\s+\+\s+(Chassis|Port)\s+ID
  ^\s+\+\s+Time\s+to\s+live
  ^\s+\+\s+System\s+capabilities
  ^Total\s+number\s+of\s+LLDP\s+neighbors
  ^\s+\+\s+802\.3
  ^\s+Advertised\s+capabilities
  # capture overflow lines
  ^\s{10,}
  ^\s+\+\s+System\s+description
  ^\s*$$
  ^. -> Error
