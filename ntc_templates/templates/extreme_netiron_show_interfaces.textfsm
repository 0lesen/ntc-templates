Value INTERFACE ([a-zA-Z0-9\/]+)
Value Required INTERFACE_STATE (up|down|disabled)
Value PROTOCOL_STATE (up|down)
Value STP_ROOT_GUARD (enabled|disabled)
Value STP_BPDU_GUARD (enabled|disabled)
Value HARDWARE_TYPE ([a-zA-Z0-9\s]+)
Value MAC_ADDRESS ([0-9a-f\.]+)
Value BUILT_IN_ADDRESS ([0-9a-f\.]+)
Value CONFIGURED_FIBER_SPEED ([0-9a-zA-Z]+)
Value CONFIGURED_COPPER_SPEED ([0-9a-zA-Z]+)
Value CONFIGURED_FIBER_DUPLEX ([0-9a-zA-Z]+)
Value CONFIGURED_COPPER_DUPLEX ([0-9a-zA-Z]+)
Value ACTUAL_SPEED ([0-9a-zA-Z]+)
Value ACTUAL_DUPLEX ([0-9a-zA-Z]+)
Value L2_PORT_STATE (Forwarding|Disabled)
Value L2_PORT_MODE (tagged|untagged|dual)
Value UNTAGGED_VLAN (\d+)
Value NUMBER_TAGGED_VLANS (\d+)
Value STP_CONFIGURED_STATE (ON|OFF)
Value PRIORITY ([a-zA-Z0-9]+)
Value FLOW_CONTROL_STATE (enabled|disabled)
Value PRIORITY_FORCED_STATE (enabled|disabled)
Value DROP_PRECEDENCE_LEVEL (\d+)
Value DROP_PRECEDENCE_FORCE_STATE (enabled|disabled)
Value DHCP_SNOOPING_TRUST (ON|OFF)
Value MIRROR_STATE (enabled|disabled)
Value MONITOR_STATE (enabled|disabled)
Value LACP_BPDU_FORWARD (Enabled|Disabled)
Value LLDP_BPDU_FORWARD (Enabled|Disabled)
Value ACTIVE_LAG_PORTS ([0-9\/\-\,]+)
Value ACTIVE_LAG_ROLE (primary|secondary)
Value ACTIVE_LAG_PRIMARY_PORT ([0-9\/]+)
Value CONFIGURED_LAG_PORTS ([0-9\/\-\,]+)
Value CONIFGURED_LAG_ROLE (primary|secondary)
Value CONFIGURED_LAG_PRIMARY_PORT ([0-9\/]+)
Value PORT_NAME ([a-zA-Z0-9\.\ \"\(\)\#\/\-]+)
Value L2_MTU_BYTES (\d+)
Value ENCAPSULATION ([a-zA-Z0-9]+)
Value OPENFLOW_OP_STATE (Disabled|Enabled)
Value OPENFLOW_INDEX (\d+)
Value CLUSTER_L2_PROTOCOL_FORWARDING (disabled|enabled)
Value LOAD_INTERVAL (\d+)
Value INPUT_BITS_SEC (\d+)
Value INPUT_PACKETS_SEC (\d+)
Value INPUT_PERCENT_UTILIZATION ([\d\.]+)
Value OUTPUT_BITS_SEC (\d+)
Value OUTPUT_PACKETS_SEC (\d+)
Value OUTPUT_PERCENT_UTILIZATION ([\d\.]+)
Value INPUT_PACKETS_TOTAL (\d+)
Value INPUT_BYTES_TOTAL (\d+)
Value INPUT_NO_BUFFERS (\d+)
Value INPUT_BROADCASTS (\d+)
Value INPUT_MULTICASTS (\d+)
Value INPUT_UNICASTS (\d+)
Value INPUT_ERRORS (\d+)
Value INPUT_CRC_ERRORS (\d+)
Value INPUT_FRAME_ERRORS (\d+)
Value INPUT_IGNORED_ERRORS (\d+)
Value INPUT_RUNTS (\d+)
Value INPUT_GIANTS (\d+)
Value INPUT_NP_RECEIVED_PACKETS (\d+)
Value INPUT_SENT_TO_TM_PACKETS (\d+)
Value INPUT_NP_INGRESS_DROPPED_PACKETS (\d+)
Value OUTPUT_PACKETS_TOTAL (\d+)
Value OUTPUT_BYTES_TOTAL (\d+)
Value OUTPUT_UNDERRUNS (\d+)
Value OUTPUT_BROADCASTS (\d+)
Value OUTPUT_MULTICASTS (\d+)
Value OUTPUT_UNICASTS (\d+)
Value OUTPUT_ERRORS (\d+)
Value OUTPUT_COLLISIONS (\d+)
Value OUTPUT_NP_TRANSMITTED_PACKETS (\d+)
Value OUTPUT_RECEIVED_FROM_TM_PACKETS (\d+)
Value L3_MTU_BYTES (\d+)
Value IP_ADDRESS ([0-9\.\/]+)
Value IP_ENCAPSULATION ([a-zA-Z]+)

Start
  ^\w -> Continue.Record
  ^${INTERFACE} is ${INTERFACE_STATE}, line protocol is ${PROTOCOL_STATE}
  ^\s+Hardware is ${HARDWARE_TYPE}
  ^\s+Hardware is ${HARDWARE_TYPE}, address is ${MAC_ADDRESS} \(bia ${BUILT_IN_ADDRESS}\)
  ^\s+STP Root Guard is ${STP_ROOT_GUARD}, STP BPDU Guard is ${STP_BPDU_GUARD}
  ^\s+Configured speed ${CONFIGURED_FIBER_SPEED}, actual ${ACTUAL_SPEED}, configured duplex ${CONFIGURED_FIBER_DUPLEX}, actual ${ACTUAL_DUPLEX}
  ^\s+Configured fiber speed ${CONFIGURED_FIBER_SPEED}, configured copper speed ${CONFIGURED_COPPER_SPEED}, actual ${ACTUAL_SPEED}, configured fiber duplex ${CONFIGURED_FIBER_DUPLEX}, configured copper duplex ${CONFIGURED_COPPER_DUPLEX}, actual ${ACTUAL_DUPLEX}
  ^\s+Member of VLAN ${UNTAGGED_VLAN} \(untagged\), port is in ${L2_PORT_MODE} mode, port state is ${L2_PORT_STATE}
  ^\s+Member of Control VLAN 4095, ${NUMBER_TAGGED_VLANS} L2 VLAN\(S\) \(tagged\), port is in ${L2_PORT_MODE} mode, port state is ${L2_PORT_STATE}
  ^\s+Member of Control VLAN 4095, VLAN ${UNTAGGED_VLAN} \(untagged\), port is in ${L2_PORT_MODE} mode, port state is ${L2_PORT_STATE}
  ^\s+Member of Control VLAN 4095, VLAN ${UNTAGGED_VLAN} \(untagged\), ${NUMBER_TAGGED_VLANS} L2 VLANS \(tagged\),
  ^\s+port is in ${L2_PORT_MODE} mode, port state is ${L2_PORT_STATE}
  ^\s+Type is Vlan \(Vlan Id: ${UNTAGGED_VLAN}\)
  ^\s+STP configured to ${STP_CONFIGURED_STATE}, Priority is ${PRIORITY}, flow control ${FLOW_CONTROL_STATE}
  ^\s+Priority force ${PRIORITY_FORCED_STATE}, Drop precedence level ${DROP_PRECEDENCE_LEVEL}, Drop precedence force ${DROP_PRECEDENCE_FORCE_STATE}
  ^\s+dhcp-snooping-trust configured to ${DHCP_SNOOPING_TRUST}
  ^\s+mirror ${MIRROR_STATE}, monitor ${MONITOR_STATE}
  ^\s+LACP BPDU Forwarding:${LACP_BPDU_FORWARD}
  ^\s+LLDP BPDU Forwarding:${LLDP_BPDU_FORWARD}
  ^\s+Member of active trunk ports ${ACTIVE_LAG_PORTS}, ${ACTIVE_LAG_ROLE} port, primary port is ${ACTIVE_LAG_PRIMARY_PORT}
  ^\s+Member of configured trunk ports ${CONFIGURED_LAG_PORTS}, ${CONIFGURED_LAG_ROLE} port, primary port is ${CONFIGURED_LAG_PRIMARY_PORT}
  ^\s+Member of active trunk ports ${ACTIVE_LAG_PORTS}, ${ACTIVE_LAG_ROLE} port
  ^\s+Member of configured trunk ports ${CONFIGURED_LAG_PORTS}, ${CONIFGURED_LAG_ROLE} port
  ^\s+Port name is ${PORT_NAME}
  ^\s+MTU ${L2_MTU_BYTES} bytes, encapsulation ${ENCAPSULATION}
  ^\s+Openflow: ${OPENFLOW_OP_STATE}, Openflow Index ${OPENFLOW_INDEX}
  ^\s+Cluster L2 protocol forwarding ${CLUSTER_L2_PROTOCOL_FORWARDING}
  ^\s+${LOAD_INTERVAL} second input rate: ${INPUT_BITS_SEC} bits/sec, ${INPUT_PACKETS_SEC} packets/sec, ${INPUT_PERCENT_UTILIZATION}% utilization
  ^\s+(\d+) second output rate: ${OUTPUT_BITS_SEC} bits/sec, ${OUTPUT_PACKETS_SEC} packets/sec, ${OUTPUT_PERCENT_UTILIZATION}% utilization
  ^\s+${INPUT_PACKETS_TOTAL} packets input, ${INPUT_BYTES_TOTAL} bytes, ${INPUT_NO_BUFFERS} no buffer
  ^\s+Received ${INPUT_BROADCASTS} broadcasts, ${INPUT_MULTICASTS} multicasts, ${INPUT_UNICASTS} unicasts
  ^\s+${INPUT_ERRORS} input errors, ${INPUT_CRC_ERRORS} CRC, ${INPUT_FRAME_ERRORS} frame, ${INPUT_IGNORED_ERRORS} ignored
  ^\s+${INPUT_RUNTS} runts, ${INPUT_GIANTS} giants
  ^\s+NP received ${INPUT_NP_RECEIVED_PACKETS} packets, Sent to TM ${INPUT_SENT_TO_TM_PACKETS} packets
  ^\s+NP Ingress dropped ${INPUT_NP_INGRESS_DROPPED_PACKETS} packets
  ^\s+${OUTPUT_PACKETS_TOTAL} packets output, ${OUTPUT_BYTES_TOTAL} bytes, ${OUTPUT_UNDERRUNS} underruns
  ^\s+Transmitted ${OUTPUT_BROADCASTS} broadcasts, ${OUTPUT_MULTICASTS} multicasts, ${OUTPUT_UNICASTS} unicasts
  ^\s+${OUTPUT_ERRORS} output errors, ${OUTPUT_COLLISIONS} collisions
  ^\s+NP transmitted ${OUTPUT_NP_TRANSMITTED_PACKETS} packets, Received from TM ${OUTPUT_RECEIVED_FROM_TM_PACKETS} packets
  ^\s+IP MTU ${L3_MTU_BYTES} bytes, encapsulation ${IP_ENCAPSULATION}
  ^\s+Internet address is ${IP_ADDRESS}, IP MTU ${L3_MTU_BYTES} bytes, encapsulation ${IP_ENCAPSULATION}
  ^\s+Not\s+member\s+of
  ^\s+Port\s+is\s+not\s+enabled
  ^\s+No\s+port\s+name
  ^\s+Vlan\s+id:\s+\d+
  ^\s*$$
  ^. -> Error
