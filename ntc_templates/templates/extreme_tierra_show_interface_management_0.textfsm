Value Key,Required INTERFACE (\S+\s\S+)
Value ADMIN_STATUS (\S+)
Value OPERATIONAL_STATUS (\S+)
Value MTU (\d+)
Value HARDWARE_TYPE (\S+)
Value MAC_ADDRESS (\S+)
Value CURRENT_SPEED (\S+)
Value DHCPV4_STATE ([\S]+)
Value DHCPV4_ADDRESS ([\S\s]+)
Value IPV4_ADDRESS (\S+)
Value IPV4_GATEWAY (\S+)
Value DHCPV6_STATE ([\S]+)
Value DHCPV6_ADDRESS ([\S\s]+)
Value IPV6_ADDRESS (\S+)
Value IPV6_GATEWAY (\S+)
Value CARRIER_TRANSITION (\d+)
Value LAST_CLEAR (\S+)
Value RX_TOTAL_PACKETS (\d+)
Value RX_BROADCAST_PACKETS (\d+)
Value RX_DISCARD_PACKETS (\d+)
Value RX_ERRORS_PACKETS (\d+)
Value RX_CRC_ERRORS (\d+)
Value RX_MCAST_PACKETS (\d+)
Value RX_OCTETS (\d+)
Value TX_TOTAL_PACKETS (\d+)
Value TX_BROADCAST_PACKETS (\d+)
Value TX_DISCARD_PACKETS (\d+)
Value TX_ERRORS_PACKETS (\d+)
Value TX_MCAST_PACKETS (\d+)
Value TX_OCTETS (\d+)
Value RX_MBITS_SEC (\S+)
Value RX_PACKETS_SEC (\S+)
Value RX_PERCENT (\S+)
Value TX_MBITS_SEC (\S+)
Value TX_PACKETS_SEC (\S+)
Value TX_PERCENT (\S+)

Start
  ^\s*${INTERFACE} Admin state ${ADMIN_STATUS}\s+Operational state ${OPERATIONAL_STATUS}
  ^\s*MTU ${MTU} bytes 
  ^\s*Hardware is ${HARDWARE_TYPE}  mac address ${MAC_ADDRESS}
  ^\s*Current Speed  ${CURRENT_SPEED}
  ^\s*DHCPv4 ${DHCPV4_STATE}\s*(\[(${DHCPV4_ADDRESS}| )\])*
  ^\s*IPv4 address ${IPV4_ADDRESS}
  ^\s*IPv4 gateway ${IPV4_GATEWAY}
  ^\s*DHCPv6 ${DHCPV6_STATE}\s*(\[(${DHCPV6_ADDRESS}| )\])*
  ^\s*IPv6 address ${IPV6_ADDRESS}
  ^\s*IPv6 gateway ${IPV6_GATEWAY}
  ^Statistics -> Statistics
  ^Input: -> Input
  ^Out: -> Out
  ^Rate Info: -> Rate
  ^\s*$$
  ^. -> Error

Statistics
  ^\s*Carrier\s+Transitions:\s+${CARRIER_TRANSITION}
  ^\s*LastClear:\s+${LAST_CLEAR} -> Start
  ^\s*$$
  ^. -> Error

Input
  ^\s*Total\s+\S+:\s+${RX_TOTAL_PACKETS}
  ^\s*Broadcast\s+\S+:\s+${RX_BROADCAST_PACKETS}
  ^\s*Discard\s+\S+:\s+${RX_DISCARD_PACKETS}
  ^\s*Errors\s+\S+:\s+${RX_ERRORS_PACKETS}
  ^\s*CRC Errors:\s+${RX_CRC_ERRORS}
  ^\s*MCast\s+\S+:\s+${RX_MCAST_PACKETS}
  ^\s*Octets:\s+${RX_OCTETS} -> Start
  ^\s*$$
  ^. -> Error

Out
  ^\s*Total\s+\S+:\s+${TX_TOTAL_PACKETS}
  ^\s*Broadcast\s+\S+:\s+${TX_BROADCAST_PACKETS}
  ^\s*Discard\s+\S+:\s+${TX_DISCARD_PACKETS}
  ^\s*Errors\s+\S+:\s+${TX_ERRORS_PACKETS}
  ^\s*MCast\s+\S+:\s+${TX_MCAST_PACKETS}
  ^\s*Octets:\s+${TX_OCTETS} -> Start
  ^\s*$$
  ^. -> Error

Rate
  ^\s+Input: ${RX_MBITS_SEC} Mbits/sec, ${RX_PACKETS_SEC} pkts/sec ${RX_PERCENT}\% of line-rate
  ^\s+Output: ${TX_MBITS_SEC} Mbits/sec, ${TX_PACKETS_SEC} pkts/sec ${TX_PERCENT}\% of line-rate -> Record Start
  ^\s*$$
  ^. -> Error