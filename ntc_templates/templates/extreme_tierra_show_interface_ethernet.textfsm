Value INTERFACE (\S+\s\S+)
Value ADMIN_STATUS (\S+)
Value OPERATIONAL_STATUS (\S+)
Value INTERFACE_INDEX (\d+)
Value INTERFACE_INDEX_HEX (0x[a-fA-f\d]+)
Value MTU (\d+)
Value HARDWARE_TYPE (\S+)
Value MAC_ADDRESS (\S+)
Value CURRENT_SPEED (\d+[a-zA-Z-])
Value FEC_MODE ([a-zA-Z-]+\s*)
Value LFS_ADMIN_STATE (\S+)
Value LFS_STATUS ([\S\s]+)
Value CARRIER_TRANSITION (\d+)
Value LAST_CLEAR (\S+)
Value RX_TOTAL_PACKETS (\d+)
Value RX_BROADCAST_PACKETS (\d+)
Value RX_DISCARD_PACKETS (\d+)
Value RX_ERRORS_PACKETS (\d+)
Value RX_FCS_ERRORS (\d+)
Value RX_MCAST_PACKETS (\d+)
Value RX_OCTETS (\d+)
Value RX_UCAST_PACKETS (\d+)
Value RX_RUNTS (\d+)
Value RX_CRC_ERRORS (\d+)
Value RX_BYTE_64_PACKETS (\d+)
Value RX_BYTE_65_127_PACKETS (\d+)
Value RX_BYTE_128_255_PACKETS (\d+)
Value RX_BYTE_256_511_PACKETS (\d+)
Value RX_BYTE_512_1023_PACKETS (\d+)
Value RX_BYTE_1024_1518_PACKETS (\d+)
Value RX_JUMBO_PACKETS (\d+)
Value TX_TOTAL_PACKETS (\d+)
Value TX_BROADCAST_PACKETS (\d+)
Value TX_DISCARD_PACKETS (\d+)
Value TX_ERRORS_PACKETS (\d+)
Value TX_MCAST_PACKETS (\d+)
Value TX_OCTETS (\d+)
Value TX_UCAST_PACKETS (\d+)
Value RX_MBITS_SEC (\S+)
Value RX_PACKETS_SEC (\S+)
Value RX_PERCENT (\S+)
Value TX_MBITS_SEC (\S+)
Value TX_PACKETS_SEC (\S+)
Value TX_PERCENT (\S+)

Start
  ^\s*${INTERFACE}\s+Admin\s+state\s+${ADMIN_STATUS}\s+Operational\s+state\s+${OPERATIONAL_STATUS}
  ^\s*Interface\s+index\s+is\s+${INTERFACE_INDEX}\s+\(${INTERFACE_INDEX_HEX}\)
  ^\s*MTU\s+${MTU}\s+bytes
  ^\s*Hardware is ${HARDWARE_TYPE}\s+mac address ${MAC_ADDRESS}
  ^\s*Current Speed\s+${CURRENT_SPEED}
  ^\s*FEC\s+Mode:\s+${FEC_MODE}
  ^\s*Link Fault Signaling:\s+${LFS_ADMIN_STATE}
  ^\s*Link Fault Status:\s+${LFS_STATUS}
  ^Statistics -> Statistics
  ^Input: -> Input
  ^Input Distribution: -> Distribution
  ^Out: -> Out
  ^Rate Info: -> Rate
  ^\s*$$
  ^. -> Error

Statistics
  ^\s*Carrier\s+Transitions:\s+${CARRIER_TRANSITION}
  ^\s*LastClear:\s+${LAST_CLEAR} -> Start
  ^\s*$$
  ^. -> Error

Input
  ^\s*Total\s+\S+:\s+${RX_TOTAL_PACKETS}
  ^\s*Broadcast\s+\S+:\s+${RX_BROADCAST_PACKETS}
  ^\s*Discard\s+\S+:\s+${RX_DISCARD_PACKETS}
  ^\s*Errors\s+\S+:\s+${RX_ERRORS_PACKETS}
  ^\s*FCS Errors:\s+${RX_FCS_ERRORS}
  ^\s*MCast\s+\S+:\s+${RX_MCAST_PACKETS}
  ^\s*Octets:\s+${RX_OCTETS}
  ^\s*UCast\s+\S+:\s+${RX_UCAST_PACKETS}
  ^\s*Runt\s+\S+:\s+${RX_RUNTS}
  ^\s*CRC Errors:\s+${RX_CRC_ERRORS} -> Start
  ^\s*$$
  ^. -> Error

Distribution
  ^\s*64 byte pkts: ${RX_BYTE_64_PACKETS}
  ^\s*65\-127 byte pkts: ${RX_BYTE_65_127_PACKETS}
  ^\s*128\-255 byte pkts: ${RX_BYTE_128_255_PACKETS}
  ^\s*256\-511 byte pkts: ${RX_BYTE_256_511_PACKETS}
  ^\s*512\-1023 byte pkts: ${RX_BYTE_512_1023_PACKETS}
  ^\s*1024\-1518 byte pkts: ${RX_BYTE_1024_1518_PACKETS}
  ^\s*Jumbo pkts: ${RX_JUMBO_PACKETS} -> Start

Out
  ^\s*Total\s+\S+:\s+${TX_TOTAL_PACKETS}
  ^\s*Broadcast\s+\S+:\s+${TX_BROADCAST_PACKETS}
  ^\s*Discard\s+\S+:\s+${TX_DISCARD_PACKETS}
  ^\s*Errors\s+\S+:\s+${TX_ERRORS_PACKETS}
  ^\s*MCast\s+\S+:\s+${TX_MCAST_PACKETS}
  ^\s*Octets:\s+${TX_OCTETS}
  ^\s*UCast\s+\S+:\s+${TX_UCAST_PACKETS} -> Start
  ^\s*$$
  ^. -> Error

Rate
  ^\s+Input: ${RX_MBITS_SEC} Mbits/sec, ${RX_PACKETS_SEC} pkts/sec ${RX_PERCENT}\% of line-rate
  ^\s+Output: ${TX_MBITS_SEC} Mbits/sec, ${TX_PACKETS_SEC} pkts/sec ${TX_PERCENT}\% of line-rate -> Record Start
  ^\s*$$
  ^. -> Error
